dnl Process this file with autoconf to produce a configure script.
dnl configure.in for HHexen v1.3
AC_REVISION([configure.in 1.00])
AC_INIT(base/a_action.c)		
AC_CONFIG_HEADER(include/config.h)

dnl **** Command-line arguments ****

dnl Default values
GLHEXEN="false"
GLLIBS=""
BASELIBS=""
LIBS="-L/usr/X11R6/lib"
HAVEGL="no"
GLBASE="opengl/i_gl.cpp"
BUILDNAMES=""

AC_ARG_ENABLE(gl,
[  --enable-gl	 		Enable OpenGL mode],
[GLHEXEN="true";SDL_VERSION="1.1.2"; AC_DEFINE(RENDER3D)],
[SDL_VERSION="1.1.0"])

AC_ARG_ENABLE(demowad,
[  --enable-demowad		Enable compilation with the demo wadfile],
[ AC_DEFINE(DEMO_WAD)])

AC_ARG_ENABLE(assassin,
[  --enable-assassin		Compile HHexen with support for the assassin],
[AC_DEFINE(ASSASSIN)])

AC_SUBST(GLHEXEN)

dnl **** Don't like this one.. ****
AC_DEFINE(_REENTRANT)

AC_DEFINE(NORANGECHECKING)

dnl **** Check for some programs ****

AC_PROG_CC
AC_PROG_CPP

dnl **** Check for some libraries ****

dnl Check for pthread
AC_CHECK_LIB(pthread, pthread_join, [BASELIBS="-lpthread"])

AC_SUBST(BASELIBS)

dnl Check for SDL
AM_PATH_SDL($SDL_VERSION,
	[HAVESDL="yes";GLBASE="sdl/i_sdlgl.o";BUILDNAMES="sdl $BUILDNAMES";GLLIBS=""],
	[HAVESDL="no"]
)

AC_SUBST(GLLIBS)
AC_SUBST(SDL_OGL)
AC_SUBST(HAVESDL)
AC_SUBST(GLBASE)

AC_CHECK_LIB(dl, dlopen)
AC_CHECK_LIB(m, sqrt)

dnl Check for GL libraries

if test "$GLHEXEN" = "true"
then
    AC_CHECK_LIB(GL, glBindTexture,[LIBS="$LIBS -lGL";]
      AC_CHECK_LIB(GLU, gluOrtho2D, [HAVEGL="yes"],
        AC_CHECK_LIB(MesaGL, glBindTexture,[LIBS="$LIBS -lMesaGL";]
	  AC_CHECK_LIB(MesaGLU, gluOrtho2D, [HAVEGL="yes"], HAVEGL="no"),
	  HAVEGL="no")
	),
        AC_CHECK_LIB(MesaGL, glBindTexture,[LIBS="$LIBS -lMesaGL";]
	  AC_CHECK_LIB(MesaGLU, gluOrtho2D, [HAVEGL="yes"], HAVEGL="no"),
	HAVEGL="no")
    )
fi

dnl **** Check for gcc strength-reduce bug ****

if test "x${GCC}" = "xyes"
then
  CFLAGS="$CFLAGS -Wall"
  AC_CACHE_CHECK( "for gcc strength-reduce bug", ac_cv_c_gcc_strength_bug,
                  AC_TRY_RUN([
int main(void) {
  static int Array[[3]];
  unsigned int B = 3;
  int i;
  for(i=0; i<B; i++) Array[[i]] = i - 3;
  exit( Array[[1]] != -2 );
}],
    ac_cv_c_gcc_strength_bug="no",
    ac_cv_c_gcc_strength_bug="yes",
    ac_cv_c_gcc_strength_bug="yes") )
  if test "$ac_cv_c_gcc_strength_bug" = "yes"
  then
    CFLAGS="$CFLAGS -fno-strength-reduce"
  fi
fi

dnl **** Check for underscore on external symbols ****

AC_CACHE_CHECK("whether external symbols need an underscore prefix",
               ac_cv_c_extern_prefix,
[saved_libs=$LIBS
LIBS="conftest_asm.s $LIBS"
cat > conftest_asm.s <<EOF
	.globl _ac_test
_ac_test:
	.long 0
EOF
AC_TRY_LINK([extern int ac_test;],[if (ac_test) return 1],
            ac_cv_c_extern_prefix="yes",ac_cv_c_extern_prefix="no")
LIBS=$saved_libs])

dnl **** Check for endianness ****

AC_C_BIGENDIAN

dnl **** Check for functions ****

dnl **** Check for header files ****

AC_CHECK_HEADERS(\
	linux/cdrom.h \
	stdint.h \
	inttypes.h \
	strings.h
)
AC_HEADER_STAT()

dnl **** Check for types ****

AC_C_CONST()
AC_C_INLINE()
AC_C_CHAR_UNSIGNED()
AC_TYPE_SIZE_T()
AC_CHECK_SIZEOF(char,0)
AC_CHECK_SIZEOF(short,0)
AC_CHECK_SIZEOF(int,0)
AC_CHECK_SIZEOF(long,0)
AC_CHECK_SIZEOF(long long,0)
AC_CHECK_SIZEOF(size_t,0)

if test "$HAVEGL" = "yes"
then
  if test "$HAVESDL" = "yes"
  then
    BUILDNAMES="OpenGL (SDL)"
  else
    BUILDNAMES="OpenGL (GLX)"
  fi 
fi

dnl **** Generate output files ****

MAKE_NAME=Makefile
AC_SUBST_FILE(MAKE_RULES)

AC_OUTPUT(Makefile)

echo
echo "HHexen configuration finished."
echo "Enabled targets: $BUILDNAMES"
echo
if test "$HAVESDL" = "no"
then
  echo "It is recommended that you install SDL (http://www.libsdl.org)."
  echo "Other targets will compile, but they are no longer supported."
  echo "If SDL is installed, you will need to upgrade to version $SDL_VERSION"
  echo
fi  
echo "Type 'make' to compile."
echo
